# Copy the queries to https://query.wikidata.org/ and get the CSV export
SELECT DISTINCT ?item ?label ?className
WHERE
{
  {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q1640628.    # network service
  } UNION {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q11371.      # Internet service provider
  } UNION {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q1641122.    # online service provider
  } UNION {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q5154231.    # communications service provider
  } UNION {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q7696092.    # telecommunications service provider
  } UNION {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q105538253.  # online shop
  } UNION {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q193424.     # web service
  } UNION {
    ?item (wdt:P31|wdt:P452)/wdt:P279* wd:Q18388277.   # technology company
  }

  ?item wdt:P856 ?website.  # has a website

  ?item rdfs:label ?label.
  FILTER(LANG(?label) = "en").

  ?item wdt:P31 ?class.
  ?class rdfs:label ?className.
  FILTER(LANG(?className) = "en").
}